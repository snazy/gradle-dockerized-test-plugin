plugins {
  id "com.jfrog.bintray" version "1.6"
  id "nu.studer.plugindev" version "1.0.4"
}

group = 'com.pedjak.gradle.plugins'
version = '0.5.7'

apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'idea'

ext {
    nettyVersion = '4.1.27.Final'
}

dependencies {
    compile gradleApi()
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'org.apache.maven:maven-artifact:3.3.3'

    // Use latest version of docker-java and explicitly use the most recent version of Netty (docker-java uses an
    // old netty version)
    compile 'com.github.docker-java:docker-java:3.0.14'
    compile "io.netty:netty-codec-http:${nettyVersion}"
    compile "io.netty:netty-handler:${nettyVersion}"
    compile "io.netty:netty-handler-proxy:${nettyVersion}"
    compile "io.netty:netty-transport-native-epoll:${nettyVersion}"
    compile "io.netty:netty-transport-native-kqueue:${nettyVersion}"

    testCompile ("org.spockframework:spock-core:1.0-groovy-2.4") {
        exclude module: "groovy-all"
    }
}

plugindev {
    pluginId "com.github.pedjak.dockerized-test"
    pluginImplementationClass "com.pedjak.gradle.plugins.dockerizedtest.DockerizedTestPlugin"
    pluginDescription "Running tests inside a docker image"
    pluginLicenses "Apache-2.0"
    pluginTags "docker", 'test', 'integration-test'
    authorId "pedjak"
    authorName "Predrag Knezevic"
    authorEmail "pedjak@gmail.com"
    projectUrl "https://github.com/pedjak/gradle-dockerized-test-plugin"
    projectInceptionYear "2015"
    done() // do not omit
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

bintray {
    if (!project.hasProperty("BINTRAY_USER")) {
        project.ext.BINTRAY_USER = ""
    }

    if (!project.hasProperty("BINTRAY_API_KEY")) {
        project.ext.BINTRAY_API_KEY = ""
    }

    user = BINTRAY_USER
    key = BINTRAY_API_KEY

    pkg.repo = "gradle-plugins"
    pkg.userOrg = "pedjak"
    pkg.attributes =  ["gradle-plugin": "${project.plugindev.pluginId}:${project.group}:${project.name}"]
}

tasks.whenTaskAdded { t ->
    if (t.name == 'generatePomFileForPluginPublication') {
        jar {
            dependsOn t
            metaInf {
                from t.destination
                into "maven/$project.group/$project.name"
                rename {
                    'pom.xml'
                }
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs = []
        }
        groovy {
            srcDirs += ['src/main/java']
        }
    }
}